| Zap directory boot file
| (c) Zap Developers

If "<Zap$Dir>">"" Then Obey

| Set up directory paths and load sprites
Set Zap$Dir <Obey$Dir>
Set ZapCode$Path <Zap$Dir>.Code.
Set ZapModules$Path <Zap$Dir>.Modules.

| Find operating system version
SetEval Zap$OSVsn 500
RMEnsure UtilityModule 5.00 SetEval Zap$OSVsn 400
RMEnsure UtilityModule 4.00 SetEval Zap$OSVsn 350
RMEnsure UtilityModule 3.50 SetEval Zap$OSVsn 300
RMEnsure UtilityModule 3.10 SetEval Zap$OSVsn 200

| Load sprites
Set ZapSprites$Path <Zap$Dir>.Resources.<Zap$OSVsn>.,<Zap$Dir>.Resources.
IconSprites ZapSprites:!Sprites
Unset ZapSprites$Path

| Load ObeySupport
RMEnsure ObeySupport 0.02 RMLoad ZapCode:ObeySpt

| Boot ZapFonts and ZapUser if they're stored inside us and haven't already been booted
If "<ZapFontsSource$Path>" = "" Then IfThere BootResources:!ZapFonts Then Obey BootResources:!ZapFonts.!Boot
If "<ZapFontsSource$Path>" = "" Then IfThere <Zap$Dir>.!ZapFonts Then Obey <Zap$Dir>.!ZapFonts.!Boot
Set Alias$ZapBootUser IfThere Choices:!ZapUser Then Obey Choices:!ZapUser.!Boot
IF "<Choices$Write>" = "" Then Set Alias$ZapBootUser ""
If "<ZapUser$Path>" = "" Then ZapBootUser
Unset Alias$ZapBootUser
If "<ZapUser$Path>" = "" Then IfThere <Zap$Dir>.!ZapUser Then Obey <Zap$Dir>.!ZapUser.!Boot

| Set up Zap$Country and paths dependent upon it
IfThere ZapUser:Config.Country Then Run ZapUser:Config.Country
If "<Zap$Country>">"" Then Else If "<Sys$Country>">"" Then Set Zap$Country <Sys$Country> Else Set Zap$Country UK
Set ZapResources$Path <Zap$Dir>.Resources.<Zap$Country>.,<Zap$Dir>.Resources.UK.
Set ZapTemplates$Path ZapUser:Templates.<Zap$Country>.,ZapResources:Templates.<Zap$Country>.,ZapUser:Templates.,ZapResources:Templates.

| Boot extension modules as appropriate
Obey ZapCode:SetZapGen
WimpSlot -min 32K
Run ZapCode:Config.ConfigScan -boot
If "<Zap$Temp>" = "1" Then WimpSlot -min 128K -max 128K
If "<Zap$Temp>" = "1" Then Run ZapCode:Config.CreateConf -script ZapCode:Config.BootScript
Obey ZapGenerated:!ZapBoot