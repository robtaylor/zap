; **************************************************************************
; *                                                                        *
; *  Project: ZapMJE                Purpose: Extensions for Zap            *
; *  Module: Header.s               Purpose: Module header and startup     *
; *  © 1997 Martin Ebourne          Last update: 04-02-97                  *
; *                                                                        *
; **************************************************************************


		GBLS	|header.h|

		GET	h.Main
		GET	h.Commands
		GET	h.Common
		GET	h.Support
		GET	h.Tables
		GET	h.Version

		AREA	|!!!Header$$code|,CODE,READONLY


;  C support

		EXPORT	|_Mod$Reloc$Off|
|_Mod$Reloc$Off| EQU	0

		EXPORT	mode
mode		EQU	p_modeblk

		EXPORT	window
window		EQU	p_windowblk

		EXPORT	file
file		EQU	p_fileblk

		EXPORT	cursor
cursor		EQU	p_cursorblk

		EXPORT	ws
ws		EQU	p_wsblk

		EXPORT	zapws
zapws		EQU	p_zapwsblk



;  Module start

		ENTRY
modulebase	DCD	0			;  Start code
		DCD	init			;  Initialisation code
		DCD	0			;  Finalisation code
		DCD	0			;  Service code
		DCD	titletext		;  Title string
		DCD	helptext		;  Help text
		DCD	0			;  Command table

helptext	=	"ZapMJE",9,9
		=	version,"(00 Jan 0000)"
 [ BUILD > ""
                = " ",BUILD
  [ (BUILD:RIGHT:1 < "0" :LOR: BUILD:RIGHT:1 > "9") :LAND: patch_level <> ""
                = "-",patch_level
  ]
 |
  [ test_flag
                = " [TEST"
   [ patch_level <> ""
                = " ",patch_level
   ]
                = "]"
  |
   [ patch_level <> ""
                = " patch-",patch_level
   ]
  ]
 ]
		=	" © "
		EXPORT	authortext
authortext	=	"Martin Ebourne", 0
		ALIGN

;  Command table
		EXPORT	titletext
titletext
		=	"ZapMJE",0
		ALIGN

;  Initialisation code

zaptext		=	"Zap",0,0
		ALIGN
		LOCAL
init		FNJSR
		LDR	r2,[r12]
		CMP	r2,#0
		BNE	cantreinit$L

;  Grab workspace

		MOV	r0,#6
		LDR	r3,=ws_size
		SWI	XOS_Module
		FNRTS VS
		STR	r2,[r12]
		ADRL	r0,wsblkstore
		STR	r2,[r0]
		MOV	r0,#0
		STR	r0,[r2,#ws_selblock-ws_0]
		MOV	r0,#-1
		STR	r0,[r2,#ws_oldcwind-ws_0]

;  Check Zap's version number

		LDR	r0,=nummodes+1
		STR	r0,[r2,#ws_numinit-ws_0]
		MOV	r0,#18
		ADR	r1,zaptext
		SWI	XOS_Module
		MOVVC	r0,#12
		SWIVC	XOS_Module
		FNRTS VS
		MOV	r12,r4
		LDR	r2,[r3,#&14]			;  Help string
		ADD	r3,r3,r2
		LDRB	r0,[r3,#3]
		TEQ	r0,#'X'
		ADDEQ	r3,r3,#1
		LDRB	r0,[r3,#5]
		CMP	r0,#'9'
		RSBLSS	r0,r0,#'1'
		BHI	badzap$L
		BNE	zapok$L
		LDRB	r0,[r3,#7]
		CMP	r0,#'9'
		RSBLSS	r0,r0,#'3'
		BHI	badzap$L
		BNE	zapok$L
		LDRB	r0,[r3,#8]
		CMP	r0,#'9'
		RSBLSS	r0,r0,#'5'
		BHI	badzap$L

;  Register modes & relocate mode info blocks

zapok$L		ADRL	r1,modestable
		ADRL	r2,modulebase
		LDR	r4,=nummodes
registerloop$L	LDR	r0,[r1],#4
		ADD	r0,r0,r2
		CALL	Zap_AddMode,r6
		FNRTS VS
		LDR	r0,[r1],#4
		ADD	r0,r0,r2
		ADRL	r3,inforelocate
		MOV	r5,#i_size
relocateloop$L	LDRB	lr,[r3],#1
		TEQ	lr,#0
		LDR	lr,[r0],#4
		TEQNE	lr,#0
		ADDNE	lr,lr,r2
		STRNE	lr,[r0,#-4]
		SUBS	r5,r5,#4
		BNE	relocateloop$L
		SUBS	r4,r4,#1
		BNE	registerloop$L

;  Register commands

		ADRL	r0,extracmdtable
		CALL	Zap_AddCommands

;  Read menu file

		ADRL	r0,menufile$L
		CALL	Zap_LoadMenu,r6
		FNRTS VS
		ADD	r6,r0,#4
		ADRL	r1,modestable+4
		ADRL	r2,modulebase
		LDR	r3,=nummodes
setmenusloop$L	LDR	r4,[r1],#8
		LDR	r5,[r6],#4
		ADD	r4,r4,r2
		STR	r5,[r4,#i_menu]
		SUBS	r3,r3,#1
		BNE	setmenusloop$L
;		CALL	Zap_Free,r6


;  Start up paren background call

		BL	startparen

;  Set up debugging

;		[	$debug
;		ADR	r0,debugclose
;		SWI	XOS_CLI
;		ADR	r0,debugdelete
;		SWI	XOS_CLI
;		MOV	r0,#&83
;		ADRL	r1,debugname
;		SWI	XOS_Find
;		STR	r0,debughandle
; ADRL r0,modulebase
; WORD r0
;		]
		FNRTS

		LTORG

badzap$L	ADR	r0,needsversion$L
		SWI	OS_GenerateError		;  This kludge due to RISC OS 3 bug
cantreinit$L	ADR	r0,cantreinitmsg$L
		SWI	OS_GenerateError		;  This kludge due to RISC OS 3 bug

needsversion$L	DCD	0
		=	"ZapMJE requires Zap version 1.20 or greater to run",0
		ALIGN
cantreinitmsg$L	DCD	0
		=	"ZapMJE cannot reinitialise",0

;		[	$debug
;debugclose	=	"Pipe:Close",0
;debugdelete	=	"Remove Pipe:$.Zap.ZapMJE",0
;debugname	=	"Pipe:$.Zap.ZapMJE",0
;		EXPORT	debughandle
;		ALIGN
;debughandle	DCD	0
;		]

menufile$L	=	"ZapMJE:Menus",0


		END
