; Zap Util Command Extension
; Copyright (C) 1998 Zap Developers
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;
; Module header and init/final code etc.

; Version and build number. The version number should be
; incremented by the maintainer only, and is intended to
; correspond to a stable release. Between version increments,
; set and increment the build number. The build number is a
; string, so do the sensible thing and put your initials before
; it to identify different build trees :-)
		GBLS	version
		GBLS	build
version		SETS	"0.08"
;build		SETS	"sja-1"

; Includes
		GET	h.ZapUtil
		GET	h.Commands

; What we give to other people
		EXPORT	Module_Header
		EXPORT	Module_End
		EXPORT	Get_Workspace

; Entry point and at start of module
		AREA	|!!!Module_Header_Comes_First|,CODE,READONLY
		ENTRY

; Module header
Module_Header
Start_Code	&	0
Init_Code	&	init_code
Final_Code
		[	WorkspaceSize <> 0
		&	final_code
		|
		&	0
		]
Service_Code	&	0
Title_String	&	title_string
Help_String	&	help_string
Command_Table	&	0

; Title, version and copyright strings
; Include build number if set
title_string	=	"ZapUtil",0
help_string	=	"ZapUtil",9,9
		=	version," (00 Jan 0000)"
		[	build <> ""
		=	" [", build, "]"
		]
help_copyright	=	" © Zap Developers", 0
		ALIGN

; Initialisation code.
; Claim workspace.
; Find Zap, and register our command table.
; Initialise workspace.
init_code
		FNJSR
		[	WorkspaceSize <> 0
		MOV	r0,#6
		MOV	r3,#WorkspaceSize
		SWI	XOS_Module
		FNRTS	VS
		MOV	ws,r2			; workspace is addressed as ws from here on
		STR	ws,[r12]
		STR	ws,workspace_addr
		]

		MOV	r0,#18
		ADR	r1,zap_title
		SWI	XOS_Module
		FNRTS	VS			; find Zap
		MOV	zapws,r4		; Zap's workspace

		ADR	r0,Zap_Command_Table
		FNcall	Zap_AddCommands
		FNRTS	VS			; add our command table

		[	WorkspaceSize <> 0
		MOV	r1,#0
		MOV	r2,ws
		MOV	r3,#WorkspaceSize
		FNcall	Zap_FillWords		; zero workspace
		]

		FNRTS

zap_title	=	"Zap", 0
;		ALIGN				; kind of superfluous :-)

; Return workspace pointer in ws. This allows us to access
; our workspace from within a Zap command.
Get_Workspace
		[	WorkspaceSize <> 0
		LDR	ws,workspace_addr
		|
		MOV	ws,#0
		]
		MOV	pc,lr

; Finalisation code.
; Free up our workspace and set the private word to null.
final_code
		[	WorkspaceSize <> 0
		FNJSR
		MOV	r0,#7
		LDR	r2,[r12]
		SWI	XOS_Module		; free workspace
		FNRTS	VS
		MOV	r0,#0
		STR	r0,[r12]		; null private word
		FNRTS
		|
		MOV	pc,lr
		]

; This should really be in its own AREA. Unfortunately this causes relocation
; problems because many linkers want to put DATA, non-READONLY and similar
; AREAs at the end of the binary, which we don't want.
workspace_addr
		[	WorkspaceSize <> 0
		&	0
		]

; Last area in module (so we know where it is)
		AREA	|ZModule_End_Comes_Last|,CODE,READONLY

Module_End

		END
