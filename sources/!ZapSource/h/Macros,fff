; Macros used in Zap assembly
; $Id: Macros,fff,v 2.18 2004-04-21 18:39:25 christian Exp $

; count in-core storage
	GBLA InCoreTotal
InCoreTotal SETA 0

; Result from INSTR macro
	GBLL	InStr

	MACRO
$la	INCORE $a
InCoreTotal SETA InCoreTotal+$a
$la
	MEND

; local labels use label$l to get a local label and LOCAL to start a new
; area

	GBLA LocalCount
	GBLS l
	GBLS L
LocalCount SETA 1
l	SETS "x$LocalCount"
L	SETS "y$LocalCount"

; increment local variable number

	MACRO
	LOCAL
LocalCount SETA LocalCount+1
l	SETS "x$LocalCount"
L	SETS "y$LocalCount"
	MEND

; start of sub. If you specify a list of registers then these are stored as
; well and will be pulled on an RTS.

	GBLS StackedRegs

	MACRO
$la	FNJSR $a
StackedRegs SETS "$a"
$la
	[ StackedRegs=""
	STR R14,[R13,#-4]!
	|
	STMFD R13!,{$StackedRegs,R14}
	]
	MEND


; set to profile this function
; must be called with a label
	MACRO
$la	PROFILE
	[ "$la" = ""
	 ! "PROFILE requires a label argument"
	]

	[ Profile
	B	$la					; cope with falling through
	=	"$la",0
	ALIGN
	DCD	16 + (:LEN:"$la"+4):AND::NOT:3		; offset to function name
	DCD	0					; number of times called
	DCD	0					; start time
	DCD	0					; accumulative time
$la	STR	R14,[R13,#-4]!
	BL	profile_start
	BL	$la.__profiled
	STR	R14,[R13,#-4]!				; in case R14 is return parameter
	BL	profile_end
	LDMFD	R13!,{R14,PC}
$la.__profiled
	]
	MEND


; end of sub + pull all stacked registers

	MACRO
$la	FNRTS $cc
$la
	[ StackedRegs=""
	LDR$cc PC,[R13],#4
	|
	LDM$cc.FD R13!,{$StackedRegs,PC}
	]
	MEND

; just pulls stacked registers and R14

	MACRO
$la	FNPULL $cc
$la
	[ StackedRegs=""
	LDR$cc R14,[R13],#4
	|
	LDM$cc.FD R13!,{$StackedRegs,R14}
	]
	MEND

; end of sub saving flags + pull all stacked registers

	MACRO
$la	FNRTSS $cc
$la
	[ StackedRegs=""
	LDM$cc.FD R13!,{PC}^
	|
	LDM$cc.FD R13!,{$StackedRegs,PC}^
	]
	MEND

; end sub with an error + pull all stacked registers (must not contain R0)

	MACRO
$la	FNRTV	$cc
$la
	[ "$cc" = ""
	SEV
	FNRTS
	|
	FNPULL	$cc
	RTV	$cc
	]
	MEND

; end sub *without* an error + pull all stacked registers

	MACRO
$la	FNRTVC	$cc
$la
	[ "$cc" = ""
	CLV
	FNRTS
	|
	FNPULL  $cc
	RTVC	$cc
	]
	MEND

; end sub and set C

	MACRO
$la	FNRTCS	$cc
$la
	[ "$cc" = ""
	SEC
	FNRTS
	|
	FNPULL	$cc
	RTCS	$cc
	]
	MEND

; end sub and clear C

	MACRO
$la	FNRTCC	$cc
$la
	[ "$cc" = ""
	CLC
	FNRTS
	|
	FNPULL	$cc
	RTCC	$cc
	]
	MEND

; end sub and set Z

	MACRO
$la	FNRTZS	$cc
$la
	[ "$cc" = ""
	SEZ
	FNRTS
	|
	FNPULL	$cc
	RTZS	$cc
	]
	MEND

; end sub and clear Z

	MACRO
$la	FNRTZC	$cc
$la
	[ "$cc" = ""
	CLRZ
	FNRTS
	|
	FNPULL	$cc
	RTZC	$cc
	]
	MEND


; set V flag and return (lr)

	MACRO
$la	RTV	$cc
$la
	[ "$cc" = ""
	SEV
	MOV	PC,R14
	|
	IMPORT	ARM32_VSRet
	B$cc	ARM32_VSRet
	]
	MEND

; clear V flag and return (lr)

	MACRO
$la	RTVC	$cc
$la
	[ "$cc" = ""
	CLV
	MOV	PC,R14
	|
	IMPORT	ARM32_VCRet
	B$cc	ARM32_VCRet
	]
	MEND

; set C flag and return (lr)

	MACRO
$la	RTCS	$cc
$la
	[ "$cc" = ""
	SEC
	MOV	PC,R14
	|
	IMPORT	ARM32_CSRet
	B$cc	ARM32_CSRet
	]
	MEND

; clear C flag and return (lr)

	MACRO
$la	RTCC	$cc
$la
	[ "$cc" = ""
	CLC
	MOV	PC,R14
	|
	IMPORT	ARM32_CCRet
	B$cc	ARM32_CCRet
	]
	MEND

; set Z flag and return (lr)

	MACRO
$la	RTZS	$cc
$la
	[ "$cc" = ""
	SEZ
	MOV	PC,R14
	|
	IMPORT	ARM32_ZSRet
	B$cc	ARM32_ZSRet
	]
	MEND

; clear Z flag and return (lr)

	MACRO
$la	RTZC	$cc
$la
	[ "$cc" = ""
	CLRZ
	MOV	PC,R14
	|
	IMPORT	ARM32_ZCRet
	B$cc	ARM32_ZCRet
	]
	MEND

; define a zero terminated string

	MACRO
$la	FNS $a
$la	= $a,0
	ALIGN
	MEND

; long adds $a=$b+#$c fixed length, $c must be word aligned

	MACRO
$la	FNADD $a,$b,$c,$cc
$la	ADD$cc $a,$b,#($c:AND:&000003FF)
	ADD$cc $a,$a,#($c:AND:&FFFFFC00)
	MEND

; ADR between files. $a=destination register $b=label of the word contaning
; the module offset to ADR to. R12 must be set up. Uses R14.

	MACRO
$la	FNADR $a,$b
$la	LDR $a,[R12,#env_modstart]	; start address of the module
	LDR R14,$b			; offset in the module we want
	ADD $a,$a,R14			; address in the module we want
	MEND

; Call a Zap entry point ($a=name of entry point as in ZapLibrary)

	MACRO
$la	FNcall $a,$r
 [ "$r">""
$la	LDR $r,[R12]
	MOV R14,PC
	ADD PC,$r,#$a
 |
$la	LDR R14,[R12]
	ADD R14,R14,#$a
	STR R14,[R13,#-4]
	MOV R14,PC
	LDR PC,[R13,#-4]
 ]
	MEND

	MACRO
$la	FNcallc $a,$cc,$r
 [ "$r">""
$la	LDR$cc $r,[R12]
	MOV$cc R14,PC
	ADD$cc PC,$r,#$a
 |
$la	LDR$cc R14,[R12]
	ADD$cc R14,R14,#$a
	STR$cc R14,[R13,#-4]
	MOV$cc R14,PC
	LDR$cc PC,[R13,#-4]
 ]
	MEND

	MACRO
$la	FNjump $a,$r
 [ "$r">""
$la	LDR $r,[R12]
	ADD PC,$r,#$a
 |
$la	STMFD R13!,{R14,PC}
	LDR R14,[R12]
	ADD R14,R14,#$a
	STR R14,[R13,#4]
	LDMFD R13!,{R14,PC}
 ]
	MEND

; Produce a general purpose error sub generating the given error $a

	MACRO
$la	ERR $a
$la	ADD R0,PC,#0			; address of the error message
	B lookup_error			; return with an error
	DCD ENUM			; error number
	= "$a",0			; error message
	ALIGN
	MEND

	MACRO
$la	ERRlit $a
$la	ADD R0,PC,#8			; address of the error message
	CMPVC PC,#1<<31  		; don't use SEV so we know how
	CMNVC PC,#1<<31			;  long the sequence is
	MOV PC,R14
	DCD ENUM			; error number
	= "$a",0			; error message
	ALIGN
	MEND

; Push a register list $a onto the stack

	MACRO
$la	PUSH $a,$cc
	INSTR "$a",","
	[ :LNOT: InStr
	 INSTR "$a","-"
	]

	[ InStr
$la	 STM$cc.FD R13!,{$a}
	|
$la	 STR$cc	$a,[R13,#-4]!
	]
	MEND


; Pull a register list $a from the stack

	MACRO
$la	PULL $a,$cc
	INSTR "$a",","
	[ :LNOT: InStr
	 INSTR "$a","-"
	]

	[ InStr
$la	 LDM$cc.FD R13!,{$a}
	|
$la	 LDR$cc	$a,[R13],#4
	]
	MEND


; Pulls a register list $a from the stack, leaving the SP alone...

	MACRO
$la	PULLS $a,$cc
	INSTR "$a",","
	[ :LNOT: InStr
	 INSTR "$a","-"
	]

	[ InStr
$la	 LDM$cc.FD R13,{$a}
	|
$la	 LDR$cc	$a,[R13]
	]
	MEND

; Divide...
; Numerator, Denominator, Result, DivTmp

	MACRO
$la	DIVIDE  $a,$b,$c,$d

	; Oh, we could do with a DISTINCT <reg_list> macro ...
	ASSERT	$a <> $b
	ASSERT	$a <> $c
	ASSERT	$a <> $d
	ASSERT	$b <> $c
	ASSERT	$b <> $d
	ASSERT	$c <> $d

	LOCAL
$la     CMP     $b,#0
        MOVEQ   $b,#1
        MOV     $d,#1
div1A$l CMP     $a,$b,ASL #1
        MOVGE   $b,$b,ASL #1
        MOVGE   $d,$d,ASL #1
        BGE     div1A$l
        MOV     $c,#0
div2A$l CMP     $a,$b
        SUBGE   $a,$a,$b
        ADDGE   $c,$c,$d
        MOV     $b,$b,LSR #1
        MOVS    $d,$d,LSR #1
        BNE     div2A$l

	LOCAL

	MEND

; swap two registers
	MACRO
$la	SWAP	$r1,$r2,$cc
$la	EOR$cc	$r1,$r2,$r1		; r1 =  r2 EOR r1
	EOR$cc	$r2,$r1,$r2             ; r2 = (r2 EOR r1) EOR r2
	EOR$cc	$r1,$r2,$r1             ; r1 = (r2 EOR r1) EOR r1
	MEND


		GBLA	EmbedLocal
		GBLS	EmbedLocalS
EmbedLocal	SETA	1
EmbedLocalS	SETS	"e$EmbedLocal"

; embed a function name
; $n is the function name, and is optional if FNAME is on the label line
; $f is non-zero if functions can fall into this one (currently ignored)
	MACRO
$la	FNAME	$n,$f
	[ EMBED_FUNCTION_NAMES = {TRUE}
EmbedLocal	SETA	EmbedLocal + 1
EmbedLocalS	SETS	"e$EmbedLocal"
	 B	e$EmbedLocalS		; allow falling through
	 [ "$n" = ""
	   =	"$la",0
	   ALIGN
	   DCD	&FF000000 + (:LEN:"$la"+4):AND::NOT:3
	 |
	   =	"$n",0
	   ALIGN
	   DCD	&FF000000 + (:LEN:"$n"+4):AND::NOT:3
	 ]
e$EmbedLocalS
	]
$la
	MEND

; test if a string is inside another string
; returns result in GBLA InStr
	MACRO
	INSTR	$str, $substr
	LCLA	SubLen
	LCLA	Pos
	LCLS	TempStr
SubLen	SETA	:LEN:"$substr"
Pos	SETA	:LEN:"$str"
InStr	SETL	{FALSE}

	WHILE	(Pos >= SubLen) :LAND::LNOT: InStr
TempStr	SETS	("$str" :LEFT: Pos) :RIGHT: SubLen
	[	TempStr = "$substr"
InStr	SETL	{TRUE}
	]
Pos	SETA	Pos-1
	WEND

	MEND



;-----------------------------------------
;   manupilation of PSR flags
;

; set C.  sets    N:?  Z:?  C:1  V:0
		MACRO
$la		SEC
$la		SUBS	R0,R0,#0
		MEND

; clear C. sets   N:?  Z:?  C:0  V:0
		MACRO
$la		CLC
$la		ADDS	R0,R0,#0
		MEND

; set V.  sets    N:?  Z:0  C:?  V:1
		MACRO
$la		SEV
$la		CMPVC	PC,#1<<31
		CMNVC	PC,#1<<31
		MEND

; clear V. sets   N:?  Z:?  C:0  V:0
		MACRO
$la		CLV
$la		ADDS	R0,R0,#0
		MEND

; set Z.   sets:  N:?  Z:1  C:-  V:-
		MACRO
$la		SEZ
$la		TEQ	R0,R0
		MEND

; clear Z. sets:  N:?  Z:0  C:-  V:-
		MACRO
$la		CLRZ
$la		TEQ	PC,#0
		MEND



;-----------------------------------------
; Logging information
;
; Tim liked to use Zephyr - however this is really only useful for immediate debugging
; purposes. It also relies on Wimp Messages, and so can't be used from interrupt code,
; exception handlers and so on.
; The advantage of SysLog is that it does priorities, and is designed for background
; logging. This can sensibly be running constantly in any release of Zap (just set the logging
; level really low), or turned off (if <Zap$Log> isn't set, !SysLog won't be loaded or used).
;
; These macros return VS on error, but it is advised to clear this before returning to the Zap
; central loop.
;
; Priorities are as follows (SysLog default is 125):
;
;  0	exceptions
;  10	fatal errors
;  20	non-fatal errors
;  50	warnings
;  100	Zap status (eg: starting, stopping)
;  110  File ops (eg: loading, saving)
;  120	Window ops (eg: creation, deletion)
;  130  Font ops (these may be removed once we've figured out what's wrong here)
;  150	Minor status (eg: loading modules)
;  200  Memory management
;
; These are bases; so you can add small amounts for less important things within that class.
; (Only really relevant for warnings and less important things.)

LOG_ALWAYS	*	0
LOG_EXCEPTION	*	0
LOG_FATAL	*	10
LOG_ERROR	*	20
LOG_WARNING	*	50
LOG_STATUS	*	100
LOG_FILE	*	110
LOG_WINDOW	*	120
; Above here will be logged by default
LOG_FONT	*	130
LOG_MINOR	*	150
; The following in particular has several levels within the class.
; LOG_MEMORY gives broad strokes (DA/heap claim and kill, and things like claim_list_block).
; LOG_MEMORY + 1 gives some more detail on mass memory ops.
; LOG_MEMORY + 2 gives all claim/free/ensure/extend heap ops (warning: several per second even when idle!).
; LOG_MEMORY + 3 gives some more detail on heap ops.
LOG_MEMORY	*	200

	GBLA	LogCount
	GBLS	LC
LogCount SETA	0
LC	SETS	"log$LogCount"

	MACRO
$la	SYSLOGF	$t,$p
$la
 [ LOGGING = SYSLOG
        STMFD   R13!,{R0-R3,R8-R12,R14}
        LDR	R0,[R12,#zap_logname]
        CMP	R0,#0
        BEQ	jump$LC
        ADR     R1,text$LC
        MOV     R2,#$p
        MOV     R3,R13
        SWI     XSysLog_LogFormatted
        B       jump$LC
text$LC	=       "$t",0
        ALIGN
jump$LC	LDMFD   R13!,{R0-R3,R8-R12,R14}

LogCount SETA	LogCount+1
LC	SETS	"log$LogCount"
 ]
        MEND

	MACRO
$la	SYSLOG	$t,$p
$la
 [ LOGGING = SYSLOG
	STMFD	R13!,{R0-R2,R14}
        LDR	R0,[R12,#zap_logname]
        CMP	R0,#0
        BEQ	jump$LC
	ADR	R1,text$LC
	MOV	R2,#$p
	SWI	XSysLog_LogMessage
	B	jump$LC
text$LC	=	"$t",0
	ALIGN
jump$LC	LDMIA	R13!,{R0-R2,R14}

LogCount SETA	LogCount+1
LC	SETS	"log$LogCount"
 ]
	MEND


; Debugging call to syslog - always works regardless of contents
; of Zap$Log variable, logs to ZapDebug with default priority of 0
		GBLA	DebugCount
		GBLS	DC
DebugCount	SETA	0
DC		SETS	"log$DebugCount"

		MACRO
$la		DEBUGF	$t,$p
$la
		PUSH	"R0"
		MRS	R0,CPSR
		PUSH	"R0"
		LDR	R0,[R13,#4]
		STMFD	R13!,{R0-R3,R8-R12,R14}
		ADR	R0,dname$DC
		ADR	R1,dtext$DC
		[ "$p" = ""
		MOV	R2,#0
		|
		MOV	R2,#$p
		]
		MOV	R3,R13
		SWI	XSysLog_LogFormatted
		B	djump$DC
dtext$DC	=	"$t",0
dname$DC	=	"ZapDebug",0
		ALIGN
djump$DC	LDMFD	R13!,{R0-R3,R8-R12,R14}
		PULL	"R0"
		MSR	CPSR_f,R0
		PULL	"R0"

DebugCount	SETA	DebugCount+1
DC		SETS	"log$DebugCount"
		MEND


; end of macros

	END
