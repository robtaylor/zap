; **************************************************************************
; *                                                                        *
; *  Project: ZapMJE                Purpose: Extensions for Zap            *
; *  Module: Commands.s             Purpose: Extension commands            *
; *  © 1997 Martin Ebourne          Last update: 13-03-97                  *
; *                                                                        *
; **************************************************************************

;  Things to do:
;    Seriously improve compile (!) - give it a *command and allow
;      wimpslot etc to be set
;    Make bracket matching work on all modes & properly


		GET	h.Main
		GET	h.Assembler
		GET	h.CMode
		GET	h.Header
		GET	h.Routines
		GET	h.Support
		GET	h.Tables

		AREA	|Asm$$code|,CODE,READONLY


;  Extra command table

		EXPORT	extracmdtable
extracmdtable	DCD	extracmdtable		;  Offset of table from start of module
		DCD	cmdservice		;  Offset of service call code


		MACRO
		CMD	$name,$handler
		=	"$name",0
		ALIGN
		DCD	$handler
		MEND

;  Commands specific to my modes

		CMD	"MJE_NEXTFUNC",nextfunc
		CMD	"MJE_LASTFUNC",lastfunc
		CMD	"MJE_OPENCOMMENT",insopencomment
		CMD	"MJE_CLOSECOMMENT",insclosecomment
		CMD	"MJE_IF",insertif
		CMD	"MJE_ENDIF",insertendif
		CMD	"MJE_COMMENT",comment
		CMD	"MJE_COMMENTLINE",commentout
		CMD	"MJE_UNCOMMENTLINE",uncomment
		CMD	"MJE_REINDENT",reindentthisline
		CMD	"MJE_SWAPTOHEADER",swaptoheader
		CMD	"MJE_GENERATEHEADER",autoheader$C
		CMD	"MJE_FINDHEADER",findheader ; works elsewhere

;  Menu entries

		CMD	"MJE_PAUSE",pause
		CMD	"MJE_INDENT",indent
		CMD	"MJE_COMMENTSET",commentset
		CMD	"MJE_INDENTCOMMENTSET",indentcmntset
		CMD	"MJE_SHOWPREPROSET",preprocessorset
		CMD	"MJE_SMARTINDENTING",smartindenting
		CMD	"MJE_CSTRINGS",cstrings

		=	0

		ALIGN

		LOCAL


;  Jump to start of next function

		DCD	x_noparams			;  Flags for command

nextfunc	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	r1,m_info
		LDR	r0,[r1,#i_funcstring]
		FNPULL
		LDR	pc,[r1,#i_nextfunc]
		; fall through

;  Jump to start of next function using search string in r0

		EXPORT	nextfuncsrch
nextfuncsrch	MOV	r1,#0				;  Move cursor
		LDR	r3,c_off			;  Start offset
		MOV	r4,#1				;  Forwards
		MOV	r5,#0				;  String
		JMP	Zap_Search,r6


		LOCAL


;  Jump to start of last function

		DCD	x_noparams			;  Flags for command

lastfunc	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	r1,m_info
		LDR	r0,[r1,#i_funcstring]
		FNPULL
		LDR	pc,[r1,#i_lastfunc]
		; fall through


;  Jump to start of last function using search string in r0

		EXPORT	lastfuncsrch
lastfuncsrch	MOV	r1,#0				;  Move cursor
		LDR	r3,c_off			;  Start offset
		MOV	r4,#-1				;  Backwards
		MOV	r5,#0				;  String
		JMP	Zap_Search,r6


		LOCAL


;  Generate function browser

		EXPORT	funcbrowser
funcbrowser	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	r1,m_info
		LDR	r0,[r1,#i_funcstring]
		MOV	r1,#1				;  Buffer
		MOV	r3,#0				;  Start offset
		MOV	r4,#1				;  Forwards
		MOV	r5,#0				;  String
		FNPULL
		JMP	Zap_Search,r6


		LOCAL


;  Inserts open comment

		DCD	x_noparams			;  Flags for command

insopencomment	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	r0,m_sharedflags1
		TST	r0,#1				;  Alternative comment set
		LDR	r0,m_info
		LDREQ	r2,[r0,#i_opencoml]
		LDREQ	r3,[r0,#i_opencom]
		LDRNE	r2,[r0,#i_opencoml2]
		LDRNE	r3,[r0,#i_opencom2]
		MOV	r0,#&11
		LDR	r1,c_off
		FNPULL
		JMP	Zap_Command,r6


		LOCAL


;  Inserts close comment

		DCD	x_noparams			;  Flags for command

insclosecomment	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	r0,m_sharedflags1
		TST	r0,#1				;  Alternative comment set
		LDR	r0,m_info
		LDREQ	r2,[r0,#i_closecoml]
		LDREQ	r3,[r0,#i_closecom]
		LDRNE	r2,[r0,#i_closecoml2]
		LDRNE	r3,[r0,#i_closecom2]
		MOV	r0,#&11
		LDR	r1,c_off
		FNPULL
		JMP	Zap_Command,r6


		LOCAL


;  Inserts "#if 0\n" or " [ 1<>0\n" at start of line

		DCD	x_noparams			; Flags for command

insertif	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	r0,m_info
		LDR	r2,[r0,#i_ifl]
		LDR	r3,[r0,#i_if]
		MOV	r0,#&11
		LDR	r1,c_loff
		FNPULL
		JMP	Zap_Command,r6


		LOCAL


;  Inserts "#endif\n" or " ]" at start of line

		DCD	x_noparams			;  Flags for command

insertendif	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	r0,m_info
		LDR	r2,[r0,#i_endifl]
		LDR	r3,[r0,#i_endif]
		MOV	r0,#&11
		LDR	r1,c_loff
		FNPULL
		JMP	Zap_Command,r6

		LOCAL


;  Insert open & close comment leaving cursor in the middle

flags$L		RN	r5
info$L		RN	r6

		DCD	x_noparams			;  Flags for command

comment		FNJSR
		BL	checkmode
		FNRTS NE
		CALL	Zap_StartOp,r0
		FNRTS VS
		LDR	info$L,m_info
		LDR	flags$L,m_sharedflags1
		TST	flags$L,#1			;  Alternative comment set
		LDREQ	r2,[info$L,#i_opencoml]
		LDREQ	r3,[info$L,#i_opencom]
		LDRNE	r2,[info$L,#i_opencoml2]
		LDRNE	r3,[info$L,#i_opencom2]
		MOV	r0,#&11				;  Insert block
		LDR	r1,c_off
		CALL	Zap_Command,r4
		FNRTS VS
		ADD	r1,r1,r2
		TST	flags$L,#1
		LDREQ	r2,[info$L,#i_closecoml]
		LDREQ	r3,[info$L,#i_closecom]
		LDRNE	r2,[info$L,#i_closecoml2]
		LDRNE	r3,[info$L,#i_closecom2]
		MOV	r0,#&11
		CALL	Zap_Command,r4
		MOVVC	r0,r1
		CALL	Zap_JumpToOffset,r1,VC
		CALL	Zap_StopOp,r0,VC
		FNRTS


		LOCAL


;  Comment out line and move onto next

		DCD	x_noparams			;  Flags for command

commentout	FNJSR
		BL	checkmode
		FNRTS NE
		CALL	Zap_StartOp,r6
		LDRVC	r0,c_off
		LDRVC	r9,=e_linestart
		BLVC	callbasemode
		FNRTS VS
		MOV	r1,r0
		MOV	r0,#1
		LDR	r4,m_info
		LDR	r5,m_sharedflags1
		TST	r5,#1				;  Alternative comment set
		LDREQ	r2,[r4,#i_opencoml]
		LDREQ	r3,[r4,#i_opencom]
		LDRNE	r2,[r4,#i_opencoml2]
		LDRNE	r3,[r4,#i_opencom2]
		CALL	Zap_Command,r6
		MOVVC	r0,r1
		LDRVC	r9,=e_lineend
		BLVC	callbasemode
		FNRTS VS
		MOV	r1,r0
		MOV	r0,#1
		TST	r5,#1
		LDREQ	r2,[r4,#i_closecoml]
		LDREQ	r3,[r4,#i_closecom]
		LDRNE	r2,[r4,#i_closecoml2]
		LDRNE	r3,[r4,#i_closecom2]
		CALL	Zap_Command,r6
		ADDVC	r0,r1,r2
		ADDVC	r0,r0,#1
		CALL	Zap_JumpToOffset,r6,VC
		CALL	Zap_StopOp,r6,VC
		FNRTS


		LOCAL


;  Uncomment line and move onto next

string$L	RN	r1
length$L	RN	r2
ptr$L		RN	r3
flags$L		RN	r4
start$L		RN	r5
openlen$L	RN	r6
info$L		RN	r11

		DCD	x_noparams			;  Flags for command

uncomment	FNJSR
		BL	checkmode
		FNRTS NE
		LDR	info$L,m_info
		LDR	r0,c_off
		LDR	r9,=e_linestart
		BL	callbasemode
		FNRTS VS
		MOV	start$L,r0
		LDR	flags$L,m_sharedflags1
		TST	flags$L,#1			;  Alternative comment set
		LDREQ	string$L,[info$L,#i_opencom]
		LDREQ	length$L,[info$L,#i_opencoml]
		LDRNE	string$L,[info$L,#i_opencom2]
		LDRNE	length$L,[info$L,#i_opencoml2]
		BL	compare$L
		ADD	openlen$L,length$L,#1
		MOV	r0,ptr$L
		LDR	r9,=e_lineend
		BL	callbasemode
		FNRTS VS
		TST	flags$L,#1
		LDREQ	string$L,[info$L,#i_closecom]
		LDREQ	length$L,[info$L,#i_closecoml]
		LDRNE	string$L,[info$L,#i_closecom2]
		LDRNE	length$L,[info$L,#i_closecoml2]
		SUB	r0,r0,length$L
		ADD	r0,r0,openlen$L
		CMP	r0,ptr$L
		BLO	exit$L
		BL	compare$L
		CALL	Zap_StartOp,r0
		FNRTS VS
		MOV	r0,#&12				;  Delete block
		MOV	r1,start$L
		TST	flags$L,#1
		LDREQ	length$L,[info$L,#i_opencoml]
		LDRNE	length$L,[info$L,#i_opencoml2]
		SUB	length$L,length$L,openlen$L
		CALL	Zap_Command,r3
		MOVVC	r0,r1
		LDRVC	r9,=e_lineend
		BLVC	callbasemode
		FNRTS VS
		TST	flags$L,#1
		LDREQ	length$L,[info$L,#i_closecoml]
		LDRNE	length$L,[info$L,#i_closecoml2]
		SUB	r1,r0,length$L
		MOV	r0,#&12
		CALL	Zap_Command,r3
		MOVVC	r0,r1
		ADDVC	r0,r0,#1
		CALL	Zap_JumpToOffset,r1,VC
		CALL	Zap_StopOp,r1,VC
		FNRTS

compare$L	FNJSR
		MOV	ptr$L,r0
loop$L		MOV	r0,ptr$L
		SUBS	length$L,length$L,#1
		FNRTS MI
		ADD	ptr$L,ptr$L,#1
		BL	getchar
		LDRB	lr,[string$L],#1
		BCS	fileend$L
		TEQ	r0,lr
		BEQ	loop$L
fileend$L	TEQ	length$L,#0
		TEQEQ	lr,#' '
		BNE	pullexit$L
		FNRTS CS
		LDRB	lr,w_cr
		TEQ	r0,lr
		FNRTS EQ
pullexit$L	ADD	sp,sp,#4			;  Keep in step with stacked
exit$L		LDR	r0,c_off
		LDR	r9,=e_lineend
		BL	callbasemode
		ADDVC	r0,r0,#1
		CALL	Zap_JumpToOffset,r1,VC
		FNRTS


		LOCAL


;  Swap between header & source file

		DCD	x_noparams			;  Flags for command

swaptoheader	FNJSR
		BL	checkmode
		FNRTS NE
		BL	releasekeys
		LDR	r1,f_name
		BL	count
		CALL	Zap_Claim
		FNRTS VS
		ADD	r3,r0,#2
		BL	strcpy
finddotloop$L	LDRB	r4,[r0,#-1]!
		CMP	r0,r3
		TEQ	r4,#'.'
		BHI	finddotloop$L
		LDREQB	r4,[r0,#-2]
		TEQEQ	r4,#'.'
		BNE	nofile
		LDRB	r4,[r0,#-1]
		TEQ	r4,#'h'
		MOVNE	r4,#'h'
		BNE	toheader$L
		LDR	r4,m_info
		LDR	r4,[r4,#i_directory]
		LDRB	r4,[r4]
toheader$L	STRB	r4,[r0,#-1]
		SUB	r0,r3,#2
		CALL	Zap_FindFile
		BVS	nofile
		TEQ	r9,#0
		BEQ	nofile
		MOV	r0,#3
		CALL	Zap_OpenWindow
		BVS	exit$L
		LDR	r2,f_mark
		SUBS	r2,r2,#8
		BMI	exit$L
		STR	r2,f_mark
		CALL	Zap_GetWindOff
		LDRVC	r4,c_wind
		LDRVC	r5,c_off
		STRVC	r0,c_wind
		LDRVC	r1,f_mptr
		LDRVC	r0,[r1,r2]
		CALL	Zap_JumpToOffset,,VC
		MOVVC	r0,r4
		CALL	Zap_ConvWindOff,,VC
		MOVVC	r0,r5
		CALL	Zap_InsertMark,,VC
exit$L		SUB	r0,r3,#2
		FNPULL
		JMP	Zap_Free,r6


		LOCAL

		DCD	(3<<3) + (1<<6) ;  Flags for command

findheader	FNJSR
		CMP	R2,#6
		BEQ	pointtoblank$L
		LDRB	R14,[R0]
		CMP	R14,#'@'
		BNE	supplied$L
		LDRB	R14,[R0,#1]
		CMP	R14,#0
		BNE	supplied$L
		BL	releasekeys
		LDR	r0,c_off		;  Start offset
		LDR	r9,=e_linestart
		BL	callbasemode
		MOV	R6,R0
		LDRB	R4,w_cr
firstloop$L	MOV	R0,R6
		BL	getchar
		BCS	errormessage3$L
		ADD	R6,R6,#1
		CMP	R0,#39 ; "'"
		CMPNE	R0,#'"'
		CMPNE	R0,#32
		MOVLE	R5,R6
		CMP	R0,#'.'
		BEQ	foundfinally$L
		CMP	R0,R4
		BNE	firstloop$L
		B	errormessage3$L
foundfinally$L	SUB	R14,R6,R5
		CMP	R14,#2
		MOVLE	R5,R6
		BL	setupmjevar
		BVS	errormessage2$L
		MOV	r0,r7
		ADR	R1,UseZapMJEWork
		BL	strcpy
		MOV	R4,R0
secondloop$L	MOV	R0,R5
		BL	getchar
		BCS	errormessage2$L
		STRB	R0,[R4],#1
		ADD	R5,R5,#1
		CMP	R0,#'.'
		CMPNE	R0,#32
		BGT	secondloop$L
		MOV	R0,#0
		STRB	R0,[R4,#-1]
findtheheader$L	MOV	R0,R7
		CALL	Zap_FindFile,r1,VC
		BVS	errormessage$L
		TEQ	r9,#0
		BEQ	nofile
		MOV	r0,#3
		CALL	Zap_OpenWindow,r1,VC
		CALL	Zap_FindInput,r1,VC
		CALL	Zap_PutCaret,r1,VC
		;Zap_JumpToOffset,r1,VC
errorquit1$L	MOV	R0,R7
		CALL	Zap_Free,r1,VC
errorquit$L	BL	destroymjevar
		FNRTS


ZapMJEWorkPath  = "ZapMJEWork$Path",0
EndOfWorkPath   = ".^.^.,C:,",0
UseZapMJEWork   = "ZapMJEWork:h.",0
ptrtoblnk$L	= "@",0
s$L		= "mje_findhdr",0
notfound$L	= "mje_hdrnotfound",0
notfound2$L	= "mje_hdrnotresolv",0
		ALIGN

errormessage$L
		ADR	R0,notfound$L
		MOV	R1,#1<<29
		CALL	Zap_Warning
		B	errorquit1$L

errormessage2$L
		ADR	R0,notfound2$L
		MOV	R1,#1<<29
		CALL	Zap_Warning
		B	errorquit1$L

errormessage3$L
		ADR	R0,notfound2$L
		MOV	R1,#0
		CALL	Zap_Warning
		B	errorquit$L

supplied$L	MOV	R6,R0
		BL	setupmjevar
		BVS	errorquit1$L
		MOV	r0,r7
		ADR	R1,UseZapMJEWork
		BL	strcpy
		MOV	R1,R6
		BL	strcpy
		B	findtheheader$L

pointtoblank$L	ADR R0,s$L
		CALL Zap_MiniPromptToken
		FNRTS

		LOCAL

setupmjevar	FNJSR
		LDR	r1,f_name
		BL	count
		ADD	R0,R0,#16
		CALL	Zap_Claim
		FNRTS	VS
		MOV	r7,r0
		LDR	r1,f_name
		BL	strcpy
		ADR	R1,EndOfWorkPath
		BL	strcpy
		ADR 	R0,ZapMJEWorkPath
		MOV	R1,R7
		MOV	R2,#255
		MOV	R3,#0
		MOV	R4,#0
		SWI	XOS_SetVarVal
		FNRTS

		LOCAL

destroymjevar	FNJSR
		ADR 	R0,ZapMJEWorkPath
		MOV	R1,R7
		MOV	R2,#-1
		MOV	R3,#0
		MOV	R4,#0
		SWI	XOS_SetVarVal
		FNRTSS			; ignore any error...

		LOCAL


;  Called also from autoheader$C

; FIXME - this is awful! it assumes stacked register _across files_! - cl
		EXPORT	nofile
StackedRegs	SETS	""
nofile		SUB	r0,r3,#2
		CALL	Zap_Free,r6
		FNRTS VS
		ADR	r0,filenotfound$L
		MOV	r1,#1<<29
		FNPULL
		JMP	Zap_Warning,r6

filenotfound$L	=	"mje_notfound",0


		LOCAL


;  Menu entry for bracket match pause

		DCD	x_nor10 + x_nor8 + x_nor9 + x_wordparam + x_defaultstring + x_tickable	;  Flags for command

pause		FNJSR
		TEQ	r2,#17
		BEQ	whatstrings$L
		TEQ	r2,#15
		BEQ	tickable_tested
		TEQ	r2,#0
		FNRTS	NE
		LDR	r4,[r0]
		CMP	r4,#0
		CMPPL	r4,#&10000
		FNRTS PL
		MOV	r0,#0
		BL	checkmode
		FNRTS	NE
		STR	r4,m_matchdelay
		FNRTS

tickable$L
		LDR	r14,[r0]
		CMP	r14,#&10000
		MOVCC	r0,#0
		FNRTS CC
		MOVEQ	r0,#0
		MOVNE	r0,#2
		FNRTS

whatstrings$L
		CMP	R0,#0
		ADREQ	R0,title$L
		FNRTS	EQ
		CMP	R0,#2
		MOVEQ	R0,#4
		FNRTS	EQ
		CMP	R0,#1
		FNRTS	NE
		MOV	r0,#0
		BL	checkmode
		FNRTS	NE
		LDR	r0,m_matchdelay
		STR 	r0,local_mess1
		ADR	r0,local_mess1
		FNRTS

local_mess1	DCD 0

title$L		= "Delay (cs)",0


 ALIGN

		LOCAL


;  Menu entry for code indent

		DCD	x_nor10 + x_nor8 + x_nor9 + x_wordparam + x_defaultstring + x_tickable ;+ x_menutext			;  Flags for command

indent		FNJSR
		TEQ	r2,#17
		BEQ	whatstrings$L
		TEQ	r2,#15
		BEQ	tickable_tested
		TEQ	r2,#0
		FNRTS	NE
		LDR	r4,[r0]
		CMP	r4,#0
		CMPPL	r4,#&10000
		FNRTS PL
		MOV	r0,#0
		BL	checkmode
		FNRTS	NE
		STR	r4,m_indent$C
		FNRTS

whatstrings$L	CMP	R0,#0
		ADREQ	R0,title$L
		FNRTS	EQ
		CMP	R0,#2
		MOVEQ	R0,#4
		FNRTS	EQ
		CMP	R0,#1
		FNRTS	NE
		MOV	r0,#0
		BL	checkmode
		FNRTS	NE
		LDR	r0,m_indent$C
		STR 	r0,local_mess1
		ADR	r0,local_mess1
		FNRTS

tickable_tested	BL	maybe_grey
		MOVEQ	r0,#0
		MOVNE	r0,#2
		FNRTS

title$L		= "Depth",0

 ALIGN

;E R0 = pointer to &10000/&20000 etc...
;X
maybe_grey	STMFD	sp!,{r0-r4,lr}
		LDR	r4,[r0]
		BL	checkmode
		LDMNEFD	sp!,{r0-r4,pc}
		LDR	r0,m_info
		LDR	r3,[r0,#i_intmode]
		ADD	r3,r3,#1
		CMP	r3,r4,LSR #16
		LDMFD	sp!,{r0-r4,pc}

		LOCAL

;  Menu entry for select comment set

		DCD	x_nor10 + x_nor8 + x_nor9 + x_wordparam + x_tickable ;  Flags for command

commentset	MOV	R5,#1
setshared	MOV	R6,#:INDEX:m_sharedflags1
setcommon	FNJSR
		MOV	R4,R0
		MOV	r0,#0
		BL	checkmode
		MOVNE	R3,#0
		BNE	checkforgrey$L
		FNRTS	NE
		LDR	r0,[modeblk,R6]
		TEQ	r2,#0
		EOREQ	r0,r0,R5
		STREQ	r0,[modeblk,R6]
		ANDS	r3,r0,r5
		MOVNE	r3,#1
		MOV	R0,R4
checkforgrey$L	BL	maybe_grey
		MOVEQ	r0,#0
		MOVNE	r0,#2
		ORR	r0,r0,r3

		FNRTS

;  Menu entry for indent comment set

		DCD	x_nor10 + x_nor8 + x_nor9 + x_wordparam + x_tickable

indentcmntset	MOV	R5,#1
		MOV	R6,#:INDEX:m_flags1$C
		B	setcommon

;  Menu entry for show preprocessor set

		LOCAL

		DCD	x_nor10 + x_nor8 + x_nor9 + x_wordparam + x_tickable

preprocessorset	MOV	R5,#2
		MOV	R6,#:INDEX:m_flags1$C
setupdatecommon	FNJSR
		BL	setcommon
		CMP	R8,#0
		FNRTS	EQ
		STMFD	sp!,{r0}
		CALL	Zap_UpdateWholeWindow
		LDMFD	sp!,{r0}
		FNRTS

;  Menu entry for smart indenting

		LOCAL

		DCD	x_nor10 + x_nor8 + x_nor9 + x_wordparam + x_tickable

smartindenting	MOV	R5,#4
		MOV	R6,#:INDEX:m_flags1$C
		B	setcommon

;  Menu entry for C strings

		LOCAL

		DCD	x_nor10 + x_nor8 + x_nor9 + x_wordparam + x_tickable

cstrings	MOV	R5,#1
		MOV	R6,#:INDEX:m_flags1$ASM
		B	setupdatecommon


;  Re-indent line

		LOCAL

		DCD	0				;  Flags for command

; FIXME: Before doing anything, it does:
;   r0 -> r2
;   r5 -> r1
;   r4 -> r0
;   r7 -> r3
; Why? checkmode preserves r0-r2, and doesn't use any inputs
; except r8. We subsequently fill r3, so MOV r3,r7 is rather
; pointless. And C_reindent doesn't use r0-r2 anyway (or r4,
; as it happens, but it's a useful thing to keep around).
; What's more, the C_reindent variable names MISMATCH what
; we're passing them. It's expecting:
;   r0 = data (nothing; this command takes no data)
;   r1 = count
;   r2 = repeat
; We give it:
;   r0 = ??? (r4 undefined on entry to commands)
;   r1 = ??? (r5 undefined on entry to commands)
;   r2 = data (nothing)
;
; This entry isn't called except as the command, so ... what
; in hell's name is going on here? - sja
reindentthisline MOV	r2,r0 ; was R7			; Huh?
		MOV	r1,r5				; Huh?
;		MOV	r2,r6
		MOV	r0,r4				; Huh?
		MOV	r3,r7				; Huh?
		MOV	r4,lr
		BL	checkmode
		MOVNE	pc,r4				; not one of our modes
		MOVEQ	lr,r4
		LDR	r3,w_format
		LDR	r4,w_flags
		ADRL	r6,C_reindent
		B	callc

		LOCAL

		END
